/// url = 'http://hl7.org/fhir/StructureMap/Contributor4to5'
/// name = 'Contributor4to5'
/// title = 'FML Conversion for Contributor: R4 to R5'

uses "http://hl7.org/fhir/4.0/StructureDefinition/Contributor" alias ContributorR4 as source
uses "http://hl7.org/fhir/StructureDefinition/Contributor" alias ContributorR5 as target

imports "http://hl7.org/fhir/StructureMap/*4to5"

conceptmap "ContributorType" {
  prefix s = "http://hl7.org/fhir/4.0/contributor-type"
  prefix t = "http://hl7.org/fhir/contributor-type"

  s:author - t:author
  s:editor - t:editor
  s:reviewer - t:reviewer
  s:endorser - t:endorser
}

group Contributor(source src : ContributorR4, target tgt : ContributorR5) extends DataType <<type+>> {
  src.type as v -> tgt.type = translate(v, '#ContributorType', 'c');
  src.name -> tgt.name;
  src.contact -> tgt.contact;
}

